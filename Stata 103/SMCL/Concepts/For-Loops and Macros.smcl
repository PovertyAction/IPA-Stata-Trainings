{smcl}
{txt}{...}
{pstd}{c TLC}{hline 43}{c TRC}{break}
{bf}{...}
{c |}{view `""Stata 103.smcl""': Innovations for Poverty Action{space 12}}{c |}{break}
{c |}{view `""Stata 103.smcl""': The Abdul Latif Jameel Poverty Action Lab{space 1}}{c |}{break}
{c |}{view `""Stata 103.smcl""':{space 43}}{c |}{break}
{c |}{view `""Stata 103.smcl""': Staff Training{space 28}}{c |}{break}
{c |}{view `""Stata 103.smcl""': Stata 103{space 33}}{c |}{break}
{sf}{...}
{c BLC}{hline 43}{c BRC}

{pstd}{bf:For-Loops and Macros}{break}

{pstd}{bf:{stata `"use "Raw/Stata 103", clear"'}}{p_end}

{pstd}{view `""SMCL/Concepts/For-Loops and Macros.smcl"##introduction"':1. Introduction}{break}
{view `""SMCL/Concepts/For-Loops and Macros.smcl"##tracing"':2. Tracing}{break}
{view `""SMCL/Concepts/For-Loops and Macros.smcl"##macros"':3. More on Macros}{break}
{view `""SMCL/Concepts/For-Loops and Macros.smcl"##loops"':4. More Loops}

{hline}{marker introduction}

{pstd}{bf:1. Introduction}

{hline}

{pstd}Let's do a quick review of for-loops and macros. Here is an example of a for-loop:

{p2colset 5 84 0 0}{...}
{pstd}{c TLC}{hline 78}{c TRC}{p_end}
{p2col:{c |} {it:It's the first time!} {bf:Try it yourself.}}{c |}{p_end}
{pstd}{c BLC}{hline 78}{c BRC}{p_end}
{p2colreset}{...}
{pstd}{...}{cmd}
foreach i of numlist 1/10 {{break}
{p_end}{pmore}display "Hello world!"{break}{p_end}{pstd}
}
{txt}{sf}{ul off}{...}

{pstd}This repeats the commands enclosed by the braces ({cmd}{ }{txt})
(here, just {cmd:display "Hello world!"})
{cmd:10} times. It's preferable to typing the command 10 times:

{pstd}{bf:{stata `"display "Hello world!""'}}{p_end}
{pstd}{bf:{stata `"display "Hello world!""'}}{p_end}
{pstd}{bf:{stata `"display "Hello world!""'}}{p_end}
{pstd}{bf:{stata `"display "Hello world!""'}}{p_end}
{pstd}{bf:{stata `"display "Hello world!""'}}{p_end}
{pstd}{bf:{stata `"display "Hello world!""'}}{p_end}
{pstd}{bf:{stata `"display "Hello world!""'}}{p_end}
{pstd}{bf:{stata `"display "Hello world!""'}}{p_end}
{pstd}{bf:{stata `"display "Hello world!""'}}{p_end}
{pstd}{bf:{stata `"display "Hello world!""'}}{p_end}
{pstd}{bf:{stata `"display "Hello world!""'}}{p_end}

{pstd}The for-loop was less code and more efficient,
and it's easier to update:
if I want to switch {cmd:"Hello world!"} to something else
or make a larger change,
I have to do it just once within the loop instead of 10 times.
The loop is also less error-prone,
because repetitively typing out the same thing can lead to mistakes.
Indeed, there are 11 {cmd:display}s above when I meant 10.

{pstd}{marker style}A quick detour into style.
Notice that after the left brace ({cmd}{{txt}), I indented once.
The code stayed indented until the right brace ({cmd}}{txt}).
This formatting makes the code more readable;
it's easy to tell what's in the loop and what's not.

{p2colset 5 84 0 0}{...}
{pstd}{c TLC}{hline 78}{c TRC}{p_end}
{p2col:{c |} {it:Technical Tip!}}{c |}{p_end}
{pstd}{c LT}{hline 78}{c RT}{p_end}
{p2col:{c |} For more tips on Stata programming style, see these resources, available on}{c |}{p_end}
{p2col:{c |} Box:}{c |}{p_end}
{p2col:{c |} }{c |}{p_end}
{p2col:{c |} {browse "http://www.stata-journal.com/sjpdf.html?articlenum=pr0018":{it:Suggestions on Stata programming style}}}{c |}{p_end}
{p2col:{c |} }{c |}{p_end}
{p2col:{c |} {browse "http://faculty.chicagobooth.edu/matthew.gentzkow/research/ra_manual_coding.pdf":{it:RA Manual: Notes on Writing Code}}}{c |}{p_end}
{pstd}{c BLC}{hline 78}{c BRC}{p_end}
{p2colreset}{...}

{pstd}For-loops and macros are distinct but related concepts.
Every for-loop is associated with its own macro, but not every macro is associated with a for-loop.
The loop above was associated with a local macro ({cmd:`i'}),
though the macro wasn't used inside the loop.
Let's look at an example of {helpb foreach:foreach}
that incorporates its macro within the loop:

{p2colset 5 84 0 0}{...}
{pstd}{c TLC}{hline 78}{c TRC}{p_end}
{p2col:{c |} {it:It's the first time!} {bf:Try it yourself.}}{c |}{p_end}
{pstd}{c BLC}{hline 78}{c BRC}{p_end}
{p2colreset}{...}
{pstd}{...}{cmd}
foreach var in sex age educ {
{p_end}{pmore}display "Checking `var' for missing values..."{p_end}{pstd}
{p_end}{pmore}list hhid `var' if `var' == .{p_end}{pstd}
}
{txt}{sf}{ul off}{...}

{pstd}This {cmd:foreach} command loops over the variables {cmd:sex}, {cmd:age}, and {cmd:educ}.
In this dataset, these variables should never have missing values.
For each variable, the loop first displays the variable name as part of a message
({cmd:"Checking `var' for missing values..."}).
It then lists all values of {cmd:hhid} for which the variable is missing.
Knowing these, we can then examine the problematic observations more closely.

{pstd}This {cmd:foreach} repeatedly sets the local macro {cmd:`var'} to each element in
the list that follows {cmd:in}, executing the commands in the loop for each value of {cmd:`var'}.
{cmd:var} is a name I made up, and I could have chosen something else.

{marker whats_in_a_name}{...}
{p2colset 5 84 0 0}{...}
{pstd}{c TLC}{hline 78}{c TRC}{p_end}
{p2col:{c |} {it:Technical Tip!}}{c |}{p_end}
{pstd}{c LT}{hline 78}{c RT}{p_end}
{p2col:{c |} {bf:What's in a name?}}{c |}{p_end}
{pstd}{c LT}{hline 78}{c RT}{p_end}
{p2col:{c |} The same rules generally apply to all Stata names, whether it's of a}{c |}{p_end}
{p2col:{c |} variable, macro, or something else. From {helpb varname:help varname}:}{c |}{p_end}
{p2col:{c |} }{c |}{p_end}
{p2col:{c |} "Variable names may be 1 to 32 characters long and must start with {cmd:a-z}, {cmd:A-Z},}{c |}{p_end}
{p2col:{c |} or {cmd:_}, and the remaining characters may be {cmd:a-z}, {cmd:A-Z}, {cmd:_}, or {cmd:0-9}."}{c |}{p_end}
{p2col:{c |} }{c |}{p_end}
{p2col:{c |} The exception to this rule is local macros, which may be 1 to 31 characters}{c |}{p_end}
{p2col:{c |} long (a character shorter), but may start with {cmd:0-9}. Global macros (which}{c |}{p_end}
{p2col:{c |} we will discuss later) follow the same naming conventions as variables.}{c |}{p_end}
{pstd}{c BLC}{hline 78}{c BRC}{p_end}
{p2colreset}{...}

{pstd}The first element in the list is {cmd:"sex"}.
Thus, the local macro {cmd:`var'} is first set to {cmd:"sex"}.
The commands in the loop are then executed:

{pstd}{cmd}{...}
display "Checking {ul:`var'} for missing values..."{break}
list hhid {ul:`var'} if {ul:`var'} == .
{txt}{sf}{ul off}{...}

{pstd}The value of {cmd:`var'} is immediately substituted for {cmd:`var'} in the commands,
so this is the same as:

{pstd}{cmd}{...}
display "Checking {ul:sex} for missing values..."{break}
list hhid {ul:sex} if {ul:sex} == .
{txt}{sf}{ul off}{...}

{pstd}After {cmd:list},
all the commands in the for-loop have been executed,
so {cmd:`var'} is set to the next element in the list: {cmd:"age"} and then finally {cmd:"educ".}

{pstd}Don't think of {cmd:display} as searching for and then accessing the contents of the local macro {cmd:`var'}.
Instead, the value of {cmd:`var'} is essentially "copied and pasted" into the command before it is executed.
Stata completes the substitution, replacing {cmd:`var'} with its value,
and {it:then} {cmd:display} is executed.
{cmd:display} has no idea that a local macro was used,
because the substitution was done before the command was executed.

{pstd}In Stata, "macros" are objects that can be substituted into commands in this way.
They are not variables;
that term is used in reference to the variables in the dataset,
which have as many values as there are observations.

{pstd}Some additional terminology: {help quotes:"double quotes"} ({cmd:""}) are used to enclose strings.
For example, {cmd:"abc"}, {cmd:"123"}, and {cmd:"Hello world"} are each a single string enclosed by double quotes.
"Single quotes" ({cmd:`} and {cmd:'}) is the term for the characters used to enclose locals,
such as {cmd:`var'}.

{hline}{marker tracing}

{pstd}{bf:2. Tracing}

{hline}

{pstd}Loops can be confusing, and they're also difficult to debug,
because for a loop that results in an error,
Stata doesn't clearly indicate which command in the loop is causing the problem.
For example, here's the loop from above,
but with a bug introduced:

{pstd}{cmd}foreach var in sex age educ {{txt}{p_end}
{pmore}{cmd}display "Checking `var' for missing values..."{txt}{p_end}
{pmore}{cmd}list hhid `var' if `var == .{txt}{p_end}
{pstd}{cmd}}{txt}{p_end}

{pstd}{stata `"run "Do/Concepts/For-Loops and Macros.do" 1"':Click here to execute.}

{pstd}To get around this, you can turn on {help trace:tracing}:

{p2colset 5 84 0 0}{...}
{pstd}{c TLC}{hline 78}{c TRC}{p_end}
{p2col:{c |} {it:It's the first time!} {bf:Try it yourself.}}{c |}{p_end}
{pstd}{c BLC}{hline 78}{c BRC}{p_end}
{p2colreset}{...}
{pstd}{...}{cmd}
set trace on
{txt}{sf}{ul off}{...}

{pstd}This gives details about the commands Stata is executing:

{pstd}{cmd}foreach var in sex age educ {{txt}{p_end}
{pmore}{cmd}display "Checking `var' for missing values..."{txt}{p_end}
{pmore}{cmd}list hhid `var' if `var == .{txt}{p_end}
{pstd}{cmd}}{txt}{p_end}

{pstd}{stata `"run "Do/Concepts/For-Loops and Macros.do" 2"':Click here to execute.}

{pstd}Once you've identified the problem:

{p2colset 5 84 0 0}{...}
{pstd}{c TLC}{hline 78}{c TRC}{p_end}
{p2col:{c |} {it:It's the first time!} {bf:Try it yourself.}}{c |}{p_end}
{pstd}{c BLC}{hline 78}{c BRC}{p_end}
{p2colreset}{...}
{pstd}{...}{cmd}
set trace off
{txt}{sf}{ul off}{...}

{pstd}Notice the behavior of {cmd:set trace on} with macros:

{pstd}{bf:{stata `"set trace on"'}}{p_end}

{pstd}{cmd}foreach var in sex age educ {{txt}{p_end}
{pmore}{cmd}display "Checking `var' for missing values..."{txt}{p_end}
{pmore}{cmd}list hhid `var' if `var' == .{txt}{p_end}
{pstd}{cmd}}{txt}{p_end}

{pstd}{stata `"run "Do/Concepts/For-Loops and Macros.do" 3"':Click here to execute.}

{pstd}{bf:{stata `"set trace off"'}}{p_end}

{pstd}{cmd:set trace on} first shows the commands as they were originally typed,
before the values of macros are substituted.
It then shows the commands
after the macros have been "expanded."
Here, we see:

{pstd}- foreach var in sex age educ {{break}
- display "Checking `var' for missing values..."{break}
= display "Checking sex for missing values..."{break}
Checking sex for missing values...{break}
- list hhid `var' if `var' == .{break}
= list hhid sex if sex == .{break}
- }

{pstd}First, we see the command as it was first typed:

{pstd}- display "Checking `var' for missing values..."

{pstd}Then the command after the macro expansion:

{pstd}= display "Checking sex for missing values..."{break}

{pstd}Then the result:

{pstd}Checking sex for missing values...

{hline}{marker macros}

{pstd}{bf:3. More on Macros}

{hline}

{pstd}A few more points about macros.

{pstd}Local macros don't exist after the do-file ends.
For example, copy these commands to a new do-file, then run it:

{pstd}{cmd}{...}
local x Hello world{break}
display "`x'"
{txt}{sf}{ul off}{...}

{pstd}Now try the following command:

{pstd}{bf:{stata `"display "`x'""'}}{p_end}

{pstd}It doesn't work, because the local doesn't exist now that the do-file is done.

{pstd}We will now briefly discuss global macros.
Basically, they're like local macros
except they stick around even after the do-file ends,
so they can be used in the Command window afterwards
or by other do-files.
Rather than being enclosed by ` and ', they
start with {cmd:$}.
Copy these commands to a new do-file, then run it:

{pstd}{cmd}{...}
global x Hello world{break}
display "$x"
{txt}{sf}{ul off}{...}

{pstd}Now try the following command:

{pstd}{bf:{stata `"display "$x""'}}{p_end}

{pstd}It works!

{p2colset 5 84 0 0}{...}
{pstd}{c TLC}{hline 78}{c TRC}{p_end}
{p2col:{c |} {it:Technical Tip!}}{c |}{p_end}
{pstd}{c LT}{hline 78}{c RT}{p_end}
{p2col:{c |} I defined {cmd:`x'} as follows:}{c |}{p_end}
{p2col:{c |} }{c |}{p_end}
{p2col:{c |} {cmd:local x Hello world}}{c |}{p_end}
{p2col:{c |} }{c |}{p_end}
{p2col:{c |} But I also could have coded:}{c |}{p_end}
{p2col:{c |} }{c |}{p_end}
{p2col:{c |} {cmd:local x "Hello world"}}{c |}{p_end}
{p2col:{c |} }{c |}{p_end}
{p2col:{c |} Here, I enclosed the value of {cmd:`x'} with double quotes. Usually, these quotes}{c |}{p_end}
{p2col:{c |} are not required. For example, they were not needed here. However, they are}{c |}{p_end}
{p2col:{c |} always allowed. The only times they are necessary are when the value of the}{c |}{p_end}
{p2col:{c |} macro begins with double quotes or a macro operator, such as {cmd:=}, or when it}{c |}{p_end}
{p2col:{c |} contains leading leading or trailing spaces. Let's look at an example where}{c |}{p_end}
{p2col:{c |} the quotes are necessary.}{c |}{p_end}
{p2col:{c |} }{c |}{p_end}
{cmd}{...}
{p2col:{c |} foreach x in "Hello world" "a" "b" "c" {c -(}}{c |}{p_end}
{p2col:{c |}{space 5}display "`x'"}{c |}{p_end}
{p2col:{c |} {c )-}}{c |}{p_end}
{txt}{sf}{ul off}{...}
{p2col:{c |} }{c |}{p_end}
{p2col:{c |} {stata `"run "Do/Text boxes.do" "for-loops and macros" 1"':Click here to execute.}}{c |}{p_end}
{p2col:{c |} }{c |}{p_end}
{p2col:{c |} The loop above {cmd:display}ed the four string values that followed {cmd:foreach in}.}{c |}{p_end}
{p2col:{c |} Let's examine a variation on this loop:}{c |}{p_end}
{p2col:{c |} }{c |}{p_end}
{cmd}{...}
{p2col:{c |} local list ""Hello world" "a" "b" "c""}{c |}{p_end}
{p2col:{c |} foreach x in `list' {c -(}}{c |}{p_end}
{p2col:{c |}{space 5}display "`x'"}{c |}{p_end}
{p2col:{c |} {c )-}}{c |}{p_end}
{txt}{sf}{ul off}{...}
{p2col:{c |} }{c |}{p_end}
{p2col:{c |} {stata `"run "Do/Text boxes.do" "for-loops and macros" 2"':Click here to execute.}}{c |}{p_end}
{p2col:{c |} }{c |}{p_end}
{p2col:{c |} We see the same output. In the definition of {cmd:`list'}, we used enclosing}{c |}{p_end}
{p2col:{c |} double quotes. Here is what would have happened if we hadn't:}{c |}{p_end}
{p2col:{c |} }{c |}{p_end}
{cmd}{...}
{p2col:{c |} local list "Hello world" "a" "b" "c"}{c |}{p_end}
{p2col:{c |} foreach x in `list' {c -(}}{c |}{p_end}
{p2col:{c |}{space 5}display "`x'"}{c |}{p_end}
{p2col:{c |} {c )-}}{c |}{p_end}
{txt}{sf}{ul off}{...}
{p2col:{c |} }{c |}{p_end}
{p2col:{c |} {stata `"run "Do/Text boxes.do" "for-loops and macros" 3"':Click here to execute.}}{c |}{p_end}
{p2col:{c |} }{c |}{p_end}
{p2col:{c |} Do you notice the difference in output? {cmd:macro list}, which shows the values}{c |}{p_end}
{p2col:{c |} of macros, is one way to see it. Here, {cmd:`list1'} is the list with the extra}{c |}{p_end}
{p2col:{c |} set of double quotes, and {cmd:`list2'} is the list without it:}{c |}{p_end}
{p2col:{c |} }{c |}{p_end}
{p2col:{c |} {bf:{stata local list1 ""Hello world" "1" "2" "3" "a" "b" "c""}}}{c |}{p_end}
{p2col:{c |} {bf:{stata local list2 "Hello world" "1" "2" "3" "a" "b" "c"}}}{c |}{p_end}
{p2col:{c |} {bf:{stata macro list _list1 _list2}}}{c |}{p_end}
{p2col:{c |} }{c |}{p_end}
{p2col:{c |} If {cmd:`list'} is defined without the extra set of double quotes, its own first}{c |}{p_end}
{p2col:{c |} and last quotes are removed. This is one of the few situations in which}{c |}{p_end}
{p2col:{c |} enclosing double quotes are required. This is because {cmd:`list'} began with a}{c |}{p_end}
{p2col:{c |} double quote. However, while not always required, the extra double quotes}{c |}{p_end}
{p2col:{c |} are always allowed.}{c |}{p_end}
{pstd}{c BLC}{hline 78}{c BRC}{p_end}
{p2colreset}{...}

{pstd}There are more advanced ways to use macros. For example:

{p2colset 5 84 0 0}{...}
{pstd}{c TLC}{hline 78}{c TRC}{p_end}
{p2col:{c |} {it:It's the first time!} {bf:Try it yourself.}}{c |}{p_end}
{pstd}{c BLC}{hline 78}{c BRC}{p_end}
{p2colreset}{...}
{pstd}{...}{cmd}
local num = 1 * 2 + 3{break}
display "`num'"
{txt}{sf}{ul off}{...}

{pstd}{cmd:local num = 1 * 2 + 3} first evaluates {cmd:1 * 2 + 3},
then stores the result in the local {cmd:`num'}.
Notice the {cmd:=} operator.

{pstd}Compare this with:

{p2colset 5 84 0 0}{...}
{pstd}{c TLC}{hline 78}{c TRC}{p_end}
{p2col:{c |} {it:It's the first time!} {bf:Try it yourself.}}{c |}{p_end}
{pstd}{c BLC}{hline 78}{c BRC}{p_end}
{p2colreset}{...}
{pstd}{...}{cmd}
local num 1 * 2 + 3{break}
display "`num'"
{txt}{sf}{ul off}{...}

{pstd}There's no {cmd:=} operator before the expression, so Stata doesn't evaluate it,
and just stores the string as-is in the local {cmd:`num'}.

{marker counting}{...}
{pstd}A common use of this feature is counting:

{p2colset 5 84 0 0}{...}
{pstd}{c TLC}{hline 78}{c TRC}{p_end}
{p2col:{c |} {it:It's the first time!} {bf:Try it yourself.}}{c |}{p_end}
{pstd}{c BLC}{hline 78}{c BRC}{p_end}
{p2colreset}{...}
{pstd}{...}{cmd}
local i 0{break}
foreach var of varlist _all {{break}
{p_end}{pmore}local i = `i' + 1{break}{p_end}{pstd}
}
{txt}{sf}{ul off}{...}

{pstd}{bf:{stata `"display "The dataset has `i' variables.""'}}{p_end}

{...}
{p2colset 5 84 0 0}{...}
{pstd}{c TLC}{hline 78}{c TRC}{p_end}
{p2col:{c |} {it:Technical Tip!}}{c |}{p_end}
{pstd}{c LT}{hline 78}{c RT}{p_end}
{p2col:{c |} {bf:More on macros}}{c |}{p_end}
{pstd}{c LT}{hline 78}{c RT}{p_end}
{p2col:{c |} If you've never read a {help manuals:Stata manual}, and would never intend to otherwise,}{c |}{p_end}
{p2col:{c |} you should still read {manlink U 18.3 Macros} in the Stata User's Guide. Especially}{c |}{p_end}
{p2col:{c |} if you are an office PA/RA, this section is a must-read about one of the}{c |}{p_end}
{p2col:{c |} most important and powerful tools in Stata: macros.}{c |}{p_end}
{p2col:{c |} }{c |}{p_end}
{p2col:{c |} Afterwards, read more about {help extended_fcn:extended macro functions} and {help macro lists},}{c |}{p_end}
{p2col:{c |} advanced macro features.}{c |}{p_end}
{pstd}{c BLC}{hline 78}{c BRC}{p_end}
{p2colreset}{...}

{hline}{marker loops}

{pstd}{bf:4. More on Loops}

{hline}

{pstd}{cmd:foreach} isn't the only loop or even the only for-loop in
Stata. If you're not already familiar with the very useful variants on {cmd:foreach in},
{cmd:foreach of varlist} and {cmd:foreach of numlist}, check out {bf:Stata 102} for examples.
{helpb forvalues} loops over certain {it:numlists}, and especially abbreviated as
{cmd:forv} or {cmd:forval}, is easier to type than {cmd:foreach of numlist}. The {helpb while} loop,
is a more advanced, but useful loop that we'll look at below:

{pstd}Instead of designating text/variables/numbers to substitute within the loop,
{cmd:while} simply sets a condition that must be met for the loop to run.
You are essentially telling Stata that if X is true, it needs to do Y.
The syntax is very simple, as follows:

{pstd}{cmd}{...}
while x = true {
{p_end}{pmore}do Y{p_end}{pstd}
}
{txt}{sf}{ul off}{...}

{pstd}Try this loop (with a local thrown in for good measure) if you feel comfortable with loops so far.
If you are already confused, it might be best to come back to it later – it’s a bit more advanced.

{pstd}{cmd}local i = 1{txt}{p_end}
{pstd}{cmd}while{space 2}`i' < 15 {{txt}{p_end}
{pmore}{cmd}display "Round `i'"{txt}{p_end}
{pmore}{cmd}local i = `i' + 1{txt}{p_end}
{pstd}{cmd}}{txt}{p_end}

{pstd}{stata `"run "Do/Concepts/For-Loops and Macros.do" 4"':Click here to execute.}

{pstd}At the beginning of the loop, `i’ was set to 1.
For the first loop, the {cmd:while} command read: if 1 < 15,
Stata would display “Round 1”, and then reset `i’ to 2 (one more than it was before).
Since the condition was in fact met, (1 is less than 15), Stata then cycled through the loop 14 times,
making `i’ first 2, then 3, etc, until `i’ hit 15.
Then, once the condition imposed on the {cmd:while} loop was no longer true
(15 is not less than 15), Stata stopped performing the operation and moved on to the next command.


{hline}

{pstd}Problem Set: {view `""SMCL/Problem sets/Concepts/For-Loops and Macros - problem set.smcl""':For-Loops and Macros}{break}

{pstd}Next: {view `""SMCL/Concepts/if.smcl""':The {bf:if} Command Versus the {bf:if} Qualifier}{break}
Previous: {view `""SMCL/Concepts/Explicit Subscripting.smcl""':Explicit Subscripting}{break}
{view `""Stata 103.smcl""':Stata 103 Start}{break}
